plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2' // https://imperceptiblethoughts.com/shadow/
    id "org.sonarqube" version "3.3"
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "io.github.rockerhieu.versionberg" version "1.0.2" // https://github.com/rockerhieu/Versionberg/
}

sonarqube {
    properties {
        property "sonar.projectKey", "MinecraftBetter_launcher_AYFOMBWtKkKwzWrfa7lu"
    }
}
javafx {
    version = "17.0.1"
    modules = ['javafx.controls', "javafx.media", "javafx.swing", "javafx.web"]
}
versionberg {
    gitDir new File(project.rootProject.rootDir, "../.git")
    major 1
    minor 1
    nameTemplate '${major}.${minor}.${commitSha}'
    codeTemplate '(((${major} * 100) + ${minor}) * 100) * 100000 + ${build}'
}

group 'fr.minecraftbetter.launcher'
version versionberg.name

jar {
    manifest {
        attributes 'Main-Class': 'fr.minecraftbetter.launcher.Main',
                'Implementation-Version': project.version
    }
}
shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    archiveBaseName.set("MinecraftBetter")
    archiveClassifier.set('')
    archiveVersion.set('')
}

repositories {
    mavenCentral()
    maven {
        url 'https://api.minecraftbetter.com/minecraftbetter/maven/'
    }
}

dependencies {
    implementation 'com.tianti:applogger:1.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation fileTree(dir: 'libs', include: '*.jar')


    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fluentui-pack:12.3.1'  // https://kordamp.org/ikonli/cheat-sheet-fluentui.html

    // JAX-B dependencies for JDK 9+
    //noinspection GradlePackageUpdate
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    //noinspection GradlePackageUpdate
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
    //noinspection GradlePackageUpdate
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'


    implementation group: 'org.jasypt', name: 'jasypt', version: '1.9.3' // https://mvnrepository.com/artifact/org.jasypt/jasypt
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0' // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'net.harawata', name: 'appdirs', version: '1.2.1' // https://mvnrepository.com/artifact/net.harawata/appdirs
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9' // https://mvnrepository.com/artifact/org.apache.commons/commons-text
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.10.0' // https://mvnrepository.com/artifact/net.lingala.zip4j/zip4j


    implementation "com.github.codeteapot.tools:maven-artifact-get:0.2.0"


    implementation 'fr.litarvan:openauth:1.1.3' // https://github.com/MinecraftBetter/OpenAuth
    implementation 'com.squareup.okhttp3:okhttp:4.10.0' // https://square.github.io/okhttp/
}

test {
    useJUnitPlatform()
}
compileJava {
    options.encoding = "UTF-8"
}
